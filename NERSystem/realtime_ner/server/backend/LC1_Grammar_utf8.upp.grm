
ICAO_ALPHABET = "ALPHA ".utf8 | "BRAVO ".utf8 | "CHARLIE ".utf8 | "DELTA ".utf8 | "ECHO ".utf8 | "FOXTROT ".utf8 | "GOLF ".utf8 | "HOTEL ".utf8 | "INDIA ".utf8 | "JULIET ".utf8 | "KILO ".utf8 | "LIMA ".utf8 | "MIKE ".utf8 | "NOVEMBER ".utf8 | "OSCAR ".utf8 | "PAPA ".utf8 | "QUEBEC ".utf8 | "ROMEO ".utf8 | "SIERRA ".utf8 | "TANGO ".utf8 | "UNIFORM ".utf8 | "VICTOR ".utf8 | "WHISKEY ".utf8 | "XRAY ".utf8 | "YANKEE ".utf8 | "ZULU ".utf8;

# DIGITS
ONE = "ONE ".utf8;
TWO = "TWO ".utf8;
THREE = "THREE ".utf8;
FOUR = "FOUR ".utf8;
FIVE = "FIVE ".utf8;
SIX = "SIX ".utf8;
SEVEN = "SEVEN ".utf8;
EIGHT = "EIGHT ".utf8;
NINE = "NINE ".utf8 | "NINER ".utf8;
ZERO  = "ZERO ".utf8;

DIGIT = ONE | TWO | THREE | FOUR | FIVE | SIX | SEVEN | EIGHT | NINE | ZERO;

LEFT = "LEFT ".utf8;
RIGHT = "RIGHT ".utf8;
CENTRE = "CENTER ".utf8 | "CENTRE ".utf8;

#AIRLINE_DESIGNATORS CURRENTLY REPRESENTS A SUBSET OF MOST COMMON CALL SIGNS FROM THE HUMAN TRANSCRIPTIONS
AIRLINE_DESIGNATOR = ("SINGAPORE ".utf8 <.265>) | ("JET ".utf8  "STAR ".utf8 "ASIA ".utf8 <.073>) | ("GO ".utf8 "CAT ".utf8 <.071>) | ("SILK ".utf8 "AIR ".utf8 <.061>) | ("RED ".utf8 "CAP ".utf8 <.035>) | ("SCOOTER ".utf8 <.027>) | ("INDONESIA ".utf8 <.027>) | (("WAGON ".utf8 | "UNION ".utf8) "AIR ".utf8 <.02>) | ("CATHAY ".utf8 <.019>) | ("MALAYSIAN ".utf8 <.015>) | ("EMIRATES ".utf8 <.015>) | ("THAI ".utf8 "ASIA ".utf8 <.014>) | ("CEBU ".utf8 <.014>) | ("LION ".utf8 <.011>) | ("QANTAS ".utf8 <.011>) | ("CHINA ".utf8 "EASTERN ".utf8 <.01>) | ("MALINDO ".utf8 <.01>) | ("SING ".utf8 "CARGO ".utf8 <.01>) | ("CHINA ".utf8 <.01>) | ("FIREFLY ".utf8 <.01>) | ("DYNASTY ".utf8 <.01>) | ("ALL ".utf8 "NIPPON ".utf8 <.008>) | ("QATARI ".utf8 <.008>) | ("JET ".utf8 "AIRWAYS ".utf8 <.007>) | ("VIET ".utf8 "NAM ".utf8 <.007>);

CALLSIGN = AIRLINE_DESIGNATOR DIGIT? DIGIT? DIGIT? DIGIT?;

# WIND IS EITHER LIGHT AND VARIABLE, CALM OR SPECIFIED WITH HEADING AND THEN SPEED, WORD SURFACE IS OPTIONAL, USED FOR LANDING AIRCRAFT
WX_WIND_PHRASE = ("SURFACE ".utf8)? "WIND ".utf8 ("LIGHT ".utf8 "AND ".utf8 "VARIABLE ".utf8 | "CALM ".utf8 | DIGIT DIGIT ZERO ("KNOTS ".utf8)? ("AT ".utf8)? DIGIT DIGIT? "DEGREES ".utf8?);
AFTER_THE_LANDING_CTL_PHRASE = ("AFTER ".utf8 "THE ".utf8 "LANDING ".utf8) CALLSIGN; 

# RUNWAYS
# EXPECT 02L AND 20R THE MOST, OTHERS WILL BE LESS USED BY LC1 POSITION
RUNWAY_02L = ("RUNWAY ".utf8)? ZERO TWO LEFT;   # TAG AS RUNWAY_02L
RUNWAY_20R = ("RUNWAY ".utf8)? TWO ZERO RIGHT;  # TAG AS RUNWAY_02R

RUNWAY_02C = ("RUNWAY ".utf8)? ZERO TWO CENTRE; # TAG AS RUNWAY_02C
RUNWAY_20C = ("RUNWAY ".utf8)? TWO ZERO CENTRE; # TAG AS RUNWAY_20C

RUNWAY_02R = ("RUNWAY ".utf8)? ZERO TWO RIGHT; # TAG AS RUNWAY_02R
RUNWAY_20L = ("RUNWAY ".utf8)? TWO ZERO LEFT;  # TAG AS RUNWAY_20L

EXPECTED_RUNWAY = RUNWAY_02L | RUNWAY_20R;
UNEXPECTED_RUNWAY = RUNWAY_02C | RUNWAY_20C;
PROBABILITY_ZERO_RUNWAY = RUNWAY_02R | RUNWAY_20L;
AIRPORT_RUNWAY = (EXPECTED_RUNWAY <0.9>) | (UNEXPECTED_RUNWAY <0.8>) | (PROBABILITY_ZERO_RUNWAY <0.2>); 

# RUNWAY USE CLEARANCES
CTL_PREFIX = ("YOU'RE ".utf8 | "YOU ".utf8 "ARE ".utf8 | "AND ".utf8);
CTL_PHRASE = CTL_PREFIX? ("CLEARED ".utf8 | "CLEAR ".utf8) ("TO ".utf8)? "LAND ".utf8;
LUAW_SUFFIX = "VIA ".utf8 "WHISKEY ".utf8;
CFT_PHRASE = ("CLEARED ".utf8 | "CLEAR ".utf8) ("FOR ".utf8)? "TAKEOFF ".utf8;
LUAW_PHRASE = ("LINE ".utf8 "UP ".utf8) ("BEHIND ".utf8)? ("AND ".utf8)? ("WAIT ".utf8)?; 

# NON RUNWAY USE CLEARANCES THAT CAN REFER TO RUNWAYS
HOLD_SHORT_PHRASE = "HOLD ".utf8 "SHORT ".utf8;
CROSS_PHRASE = "CROSS ".utf8;
CONTINUE_APPROACH_PHRASE = "CONTINUE ".utf8 "APPROACH ".utf8;

CTL_CLEARANCE = (AIRPORT_RUNWAY WX_WIND_PHRASE? CTL_PHRASE) | (CTL_PHRASEAFT AIRPORT_RUNWAY) | (AFTER_THE_LANDING_CTL_PHRASE AIRPORT_RUNWAY CTL_PHRASE) | (AIRPORT_RUNWAY AFTER_THE_LANDING_CTL_PHRASE? CTL_PHRASE);
CFT_CLEARANCE = WX_WIND_PHRASE? ((AIRPORT_RUNWAY CFT_PHRASE) | (CFT_PHRASE AIRPORT_RUNWAY));
LUAW_CLEARANCE = LUAW_PHRASE AIRPORT_RUNWAY LUAW_SUFFIX?;
HOLD_SHORT_CLEARANCE = HOLD_SHORT_PHRASE ("OF ".utf8)? (AIRPORT_RUNWAY)?;
CROSS_RUNWAY_CLEARANCE = CROSS_PHRASE AIRPORT_RUNWAY;
CONTINUE_APPROACH_CLEARANCE = (AIRPORT_RUNWAY CONTINUE_APPROACH_PHRASE) | (CONTINUE_APPROACH_PHRASE AIRPORT_RUNWAY?);

IMMEDIATE_RUNWAY_USE_CLEARANCE = CTL_CLEARANCE | CFT_CLEARANCE | LUAW_CLEARANCE | CROSS_RUNWAY_CLEARANCE;
NON_IMMEDIATE_RUNWAY_USE_CLEARANCE = HOLD_SHORT_CLEARANCE | CONTINUE_APPROACH_CLEARANCE;

# TRANSFER OF CONTROL (TOC) INSTRUCTIONS

TOC_SEP = ("AT ".utf8 |"ON ".utf8)?;   # ALLOW AT OR ON BEFORE THE FREQUENCY, BUT NOT BOTH.  DECIMAL IS OPTIONAL 

GROUND_FREQ = ("ONE ".utf8 "TWO ".utf8 "FOUR ".utf8 ("DECIMAL ".utf8)? "THREE ".utf8) | ("ONE ".utf8 "TWO ".utf8 "ONE ".utf8 ("DECIMAL ".utf8)? "EIGHT ".utf8 ("FIVE ".utf8)?) | ("ONE ".utf8 "TWO ".utf8 "ONE ".utf8 ("DECIMAL ".utf8)? "TWO ".utf8 ("SEVEN ".utf8)?);    
DEPARTURE_FREQ = "ONE ".utf8 "TWO ".utf8 "ZERO ".utf8 ("DECIMAL ".utf8)? "THREE ".utf8;
APPROACH_FREQ = "ONE ".utf8 "TWO ".utf8 "ZERO ".utf8 ("DECIMAL ".utf8)? "THREE ".utf8;
TOWER_FREQ = "ONE ".utf8 "ONE ".utf8 "EIGHT ".utf8 ("DECIMAL ".utf8)? "SIX ".utf8;
	   
# ALLOW CONTACT OR GROUND OR CONTACT GROUND, WORD SINGAPORE IS ONLY ALLOWED IF BOTH CONTACT & GROUND ARE STATED

GROUND_TOC_INSTRUCTION = "CONTACT ".utf8 ("SINGAPORE ".utf8)? "GROUND ".utf8 TOC_SEP GROUND_FREQ; 
DEPARTURE_TOC_INSTRUCTION = "CONTACT ".utf8 ("SINGAPORE ".utf8)? "DEPARTURE ".utf8 TOC_SEP DEPARTURE_FREQ;
APPROACH_TOC_INSTRUCTION = "CONTACT ".utf8 ("SINGAPORE ".utf8)? "APPROACH ".utf8 TOC_SEP APPROACH_FREQ; 
TOWER_TOC_INSTRUCTION = "CONTACT ".utf8 ("SINGAPORE ".utf8)? "TOWER ".utf8 TOC_SEP TOWER_FREQ ;

TOC_INSTRUCTION = (GROUND_TOC_INSTRUCTION <0.45>) | (DEPARTURE_TOC_INSTRUCTION <0.45>) | (APPROACH_TOC_INSTRUCTION <0.05>) | (TOWER_TOC_INSTRUCTION <0.05>);

TAXI_INSTRUCTION = "VACATE ".utf8 "RUNWAY ".utf8 | "VACATE ".utf8 "WHISKEY ".utf8 "ONE ".utf8 | "VACATE ".utf8 "WHISKEY ".utf8 "TWO ".utf8 | "VACATE ".utf8 "WHISKEY ".utf8 "THREE ".utf8 | "VACATE ".utf8 "WHISKEY ".utf8 "FOUR ".utf8 | "VACATE ".utf8 "WHISKEY ".utf8 "FIVE ".utf8 | "VACATE ".utf8 "WHISKEY ".utf8 "SIX ".utf8 | "VACATE ".utf8 "WHISKEY ".utf8 "SEVEN ".utf8 | "VACATE ".utf8 "WHISKEY ".utf8 "EIGHT ".utf8 | "VACATE ".utf8 "WHISKEY ".utf8 "NINE ".utf8 | "VACATE ".utf8 "WHISKEY ".utf8 "TEN ".utf8 | ("TAXI ".utf8 "TO ".utf8 "HOLDING ".utf8 "POINT ".utf8);

LC_CLEARANCE = IMMEDIATE_RUNWAY_USE_CLEARANCE | NON_IMMEDIATE_RUNWAY_USE_CLEARANCE;

ATC_INFORMATION = WX_WIND_PHRASE;

export COMMANDS = CALLSIGN (LC_CLEARANCE | (TAXI_INSTRUCTION GROUND_TOC_INSTRUCTION?) | TOC_INSTRUCTION  | TAXI_INSTRUCTION | ATC_INFORMATION);
